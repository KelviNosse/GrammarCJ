10. Ordered expressions
==============================================================================================================

################ DONE ################
conditional-expression:
	| null-coalescing-expression conditional-expression-p

################ DONE ################
conditional-expression-p:
	| '?' expression ':' expression 
	| EPSILON

################ DONE ################
null-coalescing-expression:
	| conditional-or-expression null-coalescing-expression-p

################ DONE ################
null-coalescing-expression-p:
	| "??" conditional-or-expression null-coalescing-expression-p
	| EPSILON

################ DONE ################
conditional-or-expression:
	| conditional-and-expression conditional-or-expression-p

################ DONE ################
conditional-or-expression-p:
	| "||" conditional-and-expression conditional-or-expression-p 
	| EPSILON

################ DONE ################
conditional-and-expression:
	| inclusive-or-expression conditional-and-expression-p

################ DONE ################
conditional-and-expression-p:
	| "&&" inclusive-or-expression conditional-and-expression-p
	| EPSILON 

################ DONE ################
inclusive-or-expression:
	| exclusive-or-expression inclusive-or-expression-p

################ DONE ################
inclusive-or-expression-p:
	| "|" exclusive-or-expression inclusive-or-expression-p
	| EPSILON

################ DONE ################
exclusive-or-expression:
	| and-expression exclusive-or-expression-p

################ DONE ################
exclusive-or-expression-p:
	| "^" and-expression exclusive-or-expression-p
	| EPSILON 

################ DONE ################
and-expression:
	| equality-expression and-expression-p

################ DONE ################
and-expression-p:
	| "&" equality-expression and-expression-p
	| EPSILON

################ DONE ################
equality-expression:
	| relational-expression equality-expression-p

################ DONE ################
equality-expression-p:
	| expression-equality-operator relational-expression equality-expression-p
	| EPSILON

################ DONE ################
relational-expression:
	| shift-expression relational-expression-p

################ DONE ################
relational-expression-p:
	| expression-relational-operator shift-expression relational-expression-p
	| is-as-operators shift-expression relational-expression-p
	| EPSILON 

################ DONE ################
shift-expression:
	| additive-expression shift-expression-p

################ DONE ################
shift-expression-p:
	| expression-shift-operator additive-expression shift-expression-p
	| EPSILON

################ DONE ################
additive-expression:
	| multiplicative-expression additive-expression-p

################ DONE ################
additive-expression-p:
	| additive-operators multiplicative-expression additive-expression-p
	| EPSILON

################ DONE ################
multiplicative-expression:
	| unary-expression mutliplicative-expression-factorized

################ DONE ################
multiplicative-expression-factorized:
	| assignment-operator expression multiplicative-expression-p
	| multiplicative-expression-p
	| EPSILON

################ DONE ################
multiplicative-expression-p:
	| multiplicative-operators unary-expression multiplicative-expression-p
	| EPSILON

9. Expressions
==============================================================================================================

################ DONE ################
unary-expression:
	| expression-unary-operator-or-increment-decrement unary-expression
	| primary-expression-or-identifier

################ DONE ################
primary-expression:
	| primary-expression-base primary-expression-p

################ DONE ################
primary-expression-or-identifier:	
	| primary-expression
	| identifier primary-expression-p

################ DONE ################
primary-expression-base:
	| "new" non-array-type instance-options
	| built-in-type
	| cast-or-parenthesized-expression
	| literal
	| "this"

################ DONE ################
instance-options: 
	| '[' instance-options-2
	| '(' argument-list ')'
	| generic

################ DONE ################
instance-options-2:
	| expression-list ']' optional-rank-specifier-list optional-array-initializer
	| optional-comma-list ']' optional-rank-specifier-list array-initializer
	
################ DONE ################
primary-expression-p:
	| '(' argument-list ')' primary-expression-p
	| '[' expression-list ']' primary-expression-p
	| identifier-attribute primary-expression-p
	| increment-decrement primary-expression-p
	| EPSILON

################ DONE ################
cast-or-parenthesized-expression:
	| '(' expression ')' optional-expression

################ DONE ################
expression-list:
	| expression expression-list-p 

################ DONE ################
expression-list-p:
	| ',' expression-list
	| EPSILON

8. Array instantiation
==============================================================================================================

################ DONE ################
optional-funct-or-array-call:
	| '(' argument-list ')'
	| optional-array-access-list 
	| EPSILON

################ DONE ################
optional-array-access-list:
	| '[' expression-list ']' optional-array-access-list
	| EPSILON

################ DONE ################
array-type:
	| non-array-type rank-specifier-list

################ DONE ################
rank-specifier-list: 
	| rank-specifier optional-rank-specifier-list

################ DONE ################
optional-rank-specifier-list:
	| rank-specifier-list
	| EPSILON 	

################ DONE ################
rank-specifier:
	| '[' optional-comma-list ']'

################ DONE ################
optional-comma-list:
	| comma-list
	| EPSILON

################ DONE ################
comma-list: 
	| ',' optional-comma-list

7. Operators
==============================================================================================================

################ DONE ################
expression-unary-operator-or-increment-decrement
	| expression-unary-operator
	| increment-decrement

################ DONE ################
expression-unary-operator:
	| '+'
	| '-'
	| "!"
	| "~"
	| "*"

################ DONE ################
is-as-operators:
	| "is"
	| "as"

################ DONE ################
multiplicative-operators:
	| '*'
	| '/'
	| '%'

################ DONE ################
additive-operators:
	| '+'
	| '-'

################ DONE ################
expression-shift-operator: 
	| "<<"
	| ">>"

################ DONE ################
expression-equality-operator:
	| "=="
	| "!="

################ DONE ################
expression-relational-operator:
	| '<'
	| '>'
	| "<="
	| ">="

################ DONE ################
assignment-operator:
	| '='
	| "+="
	| "-="
	| "*="
	| "/="
	| "%="
	| "&="
	| "|="
	| "^="
	| "<<="
	| ">>="

6. Statements support
==============================================================================================================

################ DONE ################
statement:
	| statement-options ';'
	| embedded-statement

################ DONE ################
statement-options:
	| statement-local-variable-declaration
	| identifier statement-identifier-options 
	| statement-statement-expression
	| "this" primary-expression-p variable-assigner

################ DONE ################
statement-identifier-options:
	| identifier-attribute statement-identifier-options-2

################ DONE ################
statement-identifier-options-2:
	| '[' statement-identifier-options-3
	| generic variable-declarator-list
	| variable-declarator-list
	| variable-assigner variable-declarator-list-p
	| identifier-attribute primary-expression-p statement-expression-primary-expression-options
	| '(' argument-list ')' statement-identifier-options-5
	| increment-decrement

################ DONE ################
statement-identifier-options-3:
	| optional-comma-list ']' optional-rank-specifier-list variable-declarator-list
	| expression-list ']' statement-identifier-options-4 

################ DONE ################
statement-identifier-options-4:
	| variable-assigner variable-declarator-list-p
	| primary-expression-p

################ DONE ################
statement-identifier-options-5:
	| primary-expression-p statement-expression-primary-expression-options
	| EPSILON

################ DONE ################
statement-statement-expression:
	| expression-unary-operator unary-expression assignment-operator expression
	| increment-decrement primary-expression-or-identifier

################ DONE ################
statement-local-variable-declaration:
	| built-in-type optional-rank-specifier-list variable-declarator-list
	| "var" variable-declarator-list

################ DONE ################
statement-list: 
	| statement optional-statement-list

################ DONE ################
optional-statement-list:
	| statement-list
	| EPSILON

################ DONE ################
embedded-statement:
	| maybe-empty-block
	| selection-statement
	| iteration-statement
	| jump-statement ';'

################ DONE ################
embedded-statement-or-statement-expression:
	| embedded-statement
	| statement-expression ';'

################ DONE ################
iteration-statement:
	| while-statement
	| do-statement
	| for-statement
	| foreach-statement

################ DONE ################
while-statement:
	| "while" '(' expression ')' embedded-statement-or-statement-expression

################ DONE ################
do-statement: 
	| "do" embedded-statement-or-statement-expression "while" '(' expression ')' ';'

################ DONE ################
for-statement:
	| "for" '(' optional-for-initializer ';' optional-expression ';' optional-statement-expression-list ')' embedded-statement-or-statement-expression

################ DONE ################
foreach-statement:
	| "foreach" '(' type-or-var identifier "in" expression ')' embedded-statement-or-statement-expression

################ DONE ################
jump-statement:
	| "break"
	| "continue"
	| "return" optional-expression

################ DONE ################
optional-expression: 
	| expression
	| EPSILON

################ DONE ################
optional-for-initializer:
	| statement-options statement-options-list
	| EPSILON

statement-options-list:
	| ',' statement-options statement-options-list
	| EPSILON

################ DONE ################
selection-statement:
	| if-statement
	| switch-statement

################ DONE ################
switch-statement:
	| "switch" '(' expression ')' '{' optional-switch-section-list '}'

################ DONE ################
optional-switch-section-list:
	| switch-label-list statement-list optional-switch-section-list
	| EPSILON

################ DONE ################
switch-label-list:
	| switch-label switch-label-list-p

################ DONE ################
switch-label-list-p:
	| switch-label-list
	| EPSILON
	
################ DONE ################
switch-label:
	| "case" expression ':'
	| "default" ':'

################ DONE ################
if-statement:
	| "if" '(' expression ')' embedded-statement-or-statement-expression optional-else-part

################ DONE ################
optional-else-part:
	| else-part
	| EPSILON

################ DONE ################
else-part:
	| "else" embedded-statement-or-statement-expression;

################ DONE ################
local-variable-declaration:
	| type-or-var variable-declarator-list

################ DONE ################
statement-expression:
	| primary-expression-or-identifier statement-expression-primary-expression-options
	| expression-unary-operator unary-expression assignment-operator expression
	| increment-decrement primary-expression-or-identifier

################ DONE ################
statement-expression-primary-expression-options:
	| '(' argument-list ')'
	| increment-decrement
	| assignment-operator expression

################ DONE ################
statement-expression-list:
	| statement-expression statement-expression-list-p

################ DONE ################
statement-expression-list-p:
	| ',' statement-expression statement-expression-list-p
	| EPSILON

################ DONE ################
optional-statement-expression-list:
	| statement-expression-list
	| EPSILON

################ DONE ################
increment-decrement:
	| "++"
	| "--"

5. Classes support
==============================================================================================================

################ DONE ################
literal:
	| int-literal
	| char-literal
	| float-literal
	| string-literal
	| "true"
	| "false"

################ DONE ################
expression:
    | conditional-expression

################ DONE ################
class-declaration:
	| class-modifier "class" identifier inheritance-base class-body optional-body-end

################ DONE ################
class-modifier:
	| "abstract"
	| EPSILON

################ DONE ################
class-body:
	| '{' optional-class-member-declaration-list '}'

################ DONE ################
optional-class-member-declaration-list:
	| class-member-declaration optional-class-member-declaration-list
	| EPSILON

################ DONE ################
class-member-declaration: 
	| encapsulation-modifier class-member-declaration-options

################ DONE ################
class-member-declaration-options:
    | optional-modifier type-or-void class-member-declaration-options-p

################ DONE ################
class-member-declaration-options-p:
	| identifier field-or-method-declaration
	| constructor-declaration

################ DONE ################
optional-modifier:
    | "static"
    | "virtual"
    | "override"
    | "abstract"
    | EPSILON

################ DONE ################
field-or-method-declaration:
    | field-declaration
	| method-declaration

################ DONE ################
field-declaration: 
	| variable-assigner variable-declarator-list-p ';'

################ DONE ################
variable-assigner:
	| assigment-operator variable-initializer
	| EPSILON

################ DONE ################
variable-declarator-list:
	| identifier variable-assigner variable-declarator-list-p

################ DONE ################
variable-declarator-list-p:
	| ',' variable-declarator-list
	| EPSILON

################ DONE ################
optional-array-initializer:
	| array-initializer
	| EPSILON

################ DONE ################
array-initializer: 
	| '{' optional-variable-initializer-list '}'

################ DONE ################
variable-initializer:
	| expression
	| array-initializer

################ DONE ################
variable-initializer-list:
	| variable-initializer variable-initializer-list-p

################ DONE ################
optional-variable-initializer-list:
	| variable-initializer-list
	| EPSILON

################ DONE ################
variable-initializer-list-p:
	| ',' variable-initializer-list
	| EPSILON

################ DONE ################
argument-list:
	| expression argument-list-p
	| EPSILON

################ DONE ################
argument-list-p:
	| ',' expression argument-list-p
	| EPSILON

################ DONE ################
method-declaration:
	| '(' fixed-parameters ')' maybe-empty-block

################ DONE ################
maybe-empty-block:
	| '{' optional-statement-list '}'
	| ';'

################ DONE ################
constructor-declaration:
	| constructor-declarator maybe-empty-block

################ DONE ################
constructor-declarator:
	| '(' fixed-parameters ')' constructor-initializer

################ DONE ################
constructor-initializer:
	| ':' "base" '(' argument-list ')'
	| EPSILON

################ DONE ################
type:
	| non-array-type optional-rank-specifier-list optional-generic

generic:
	| '<' type-list '>'

optional-generic:
	| generic
	| EPSILON

################ DONE ################
type-list:
	| type type-list-p

################ DONE ################
type-list-p:
	| ',' type type-list-p
	| EPSILON

################ DONE ################
non-array-type:
	| qualified-identifier
	| built-in-type

################ DONE ################
built-in-type:
	| "int"
	| "char"
	| "string"
	| "bool"
	| "float"

################ DONE ################
type-or-var:
	| type
	| "var"

################ DONE ################
type-or-void:
	| type
	| "void"

4. Interface support
==============================================================================================================

################ DONE ################
fixed-parameter:
	| type identifier

################ DONE ################
fixed-parameters:
	| fixed-parameter fixed-paramaters-p
	| EPSILON

################ DONE ################
fixed-parameters-p:
	| ',' fixed-parameter fixed-parameters-p
	| EPSILON

################ DONE ################
interface-declaration: 
	| "interface" identifier inheritance-base interface-body optional-body-end

################ DONE ################
interface-method-header:
	| type-or-void identifier '(' fixed-parameters ')' 

################ DONE ################
inheritance-base:
	| ':' identifiers-list
	| EPSILON

################ DONE ################
interface-body:
	| '{' interface-method-declaration-list '}'

################ DONE ################
interface-method-declaration-list:
	| interface-method-header ';' interface-method-declaration-list
	| EPSILON

################ DONE ################
identifiers-list:
	| qualified-identifier identifiers-list-p

################ DONE ################
identifiers-list-p:
	| ',' qualified-identifier identifiers-list-p
	| EPSILON

################ DONE ################
qualified-identifier:
	| identifier identifier-attribute

3. Enum support
==============================================================================================================

################ DONE ################
type-declaration-list:
	| type-declaration type-declaration-list
	| EPSILON

################ DONE ################
type-declaration:
	| encapsulation-modifier group-declaration

################ DONE ################
encapsulation-modifier:
	| "public"
	| "protected"
	| "private"
	| EPSILON

################ DONE ################
group-declaration:
	| class-declaration
    | interface-declaration
    | enum-declaration

################ DONE ################
enum-declaration: 
	| "enum" identifier enum-body optional-body-end

################ DONE ################
enum-body:
	| '{' optional-assignable-identifiers-list '}'

################ DONE ################
optional-body-end:
	| ';'
	| EPSILON

################ DONE ################
optional-assignable-identifiers-list:
	| identifier assignment-options
	| EPSILON

################ DONE ################
assignment-options:
	| optional-assignable-identifiers-list-p
	| '=' expression optional-assignable-identifiers-list-p
	| [EPSILON]

################ DONE ################
optional-assignable-identifiers-list-p:
	| ',' optional-assignable-identifiers-list
	| EPSILON	

2. Namespace support
==============================================================================================================

################ DONE ################
namespace-body:
	| '{' optional-using-directive optional-namespace-member-declaration '}'

################ DONE ################
namespace-declaration:
	| "namespace" qualified-identifier namespace-body

################ DONE ################
namespace-member-declaration:
	| namespace-declaration optional-namespace-member-declaration
	| type-declaration-list [optional-namespace-member-declaration]

################ DONE ################
optional-namespace-member-declaration:
	| namespace-member-declaration
	| EPSILON

1. Using directives support
==============================================================================================================

################ DONE ################
identifier-attribute:
	| '.' identifier identifier-attribute
	| EPSILON

################ DONE ################
using-directive:
	| "using" qualified-identifier ';' optional-using-directive
	
################ DONE ################
optional-using-directive:
	| using-directive
	| EPSILON

################ DONE ################
compilation-unit:
	| optional-using-directive [optional-namespace-member-declaration]
	| optional-namespace-member-declaration
	| EPSILON

################ DONE ################
code:
	| compilation-unit
